alias Suit db # hardsuit itself
alias Helmet d0 # hardsuit helmet

# setup register references
alias TempExternal r0
alias PressExternal r1
alias SafetyCheck r2 # 1 = safe, 0 = unsafe
alias HelmetOpen r3 # 1 = open, 0 = closed

# setup safety margins
define SafeTempMax 305.15 # set in Kelvins, 302.15 K = 29 C
define SafeTempMin 280.15 # 283.15 K = 10 C
define SafePressureMax 175 # both in kPa
define SafePressureMin 45

start:
 yield
 # runs safety checks and decides which mode to activate
 jal atmocheck
 beqz SafetyCheck safe
 bnez SafetyCheck danger
j start

atmocheck:
 # first checks if pressure is safe
 move SafetyCheck 0
 l PressExternal Suit PressureExternal
 slt SafetyCheck PressExternal SafePressureMin
 bnez SafetyCheck ra
 sgt SafetyCheck PressExternal SafePressureMax
 bnez SafetyCheck ra
 # then checks if temp is safe
 l TempExternal Suit TemperatureExternal
 slt SafetyCheck TempExternal SafeTempMin
 bnez SafetyCheck ra
 sgt SafetyCheck TempExternal SafeTempMax
 bnez SafetyCheck ra
j ra

danger:
# turns on suit to protect player & locks helmet
s Suit AirRelease 1
s Suit On 1
s Suit Filtration 1
s Helmet Open 0
s Helmet Lock 1
j start

safe:
# unlocks helmet to allow player to decide if it should be open
s Helmet Lock 0
l HelmetOpen Helmet Open # check if helmet open
brnez HelmetOpen 5 # turn suit on if closed, off if open
s Suit AirRelease 1
s Suit On 1
s Suit Filtration 1
jr 4
s Suit AirRelease 0
s Suit On 0
s Suit Filtration 0
j start